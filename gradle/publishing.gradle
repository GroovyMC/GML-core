import groovy.transform.CompileStatic
import org.w3c.dom.Document
import org.w3c.dom.Element

modrinth {
    token = findProperty('modrinthToken') ?: System.getenv('MODRINTH_TOKEN')
    projectId = project.modrinth_project
    versionNumber = project.version
    versionType = project.ext.versionBasedReleaseType
    uploadFile = tasks.fullJar
    gameVersions = [mc_version]
    loaders = ['forge']
    changelog.set(tasks.changelog.output.map {
        it.asFile.text
    })
}
tasks.named('modrinth') {
    dependsOn(tasks.fullJar, tasks.changelog)
}

publishCurseForge {
    apiToken = findProperty('curseforgeKey') ?: System.getenv('CURSEFORGE_TOKEN')
    group = 'publishing'
    disableVersionDetection()

    final projectId = findProperty('curseforge_project')
    final modFile = upload(projectId, tasks.fullJar)
    modFile.changelog = tasks.changelog.output.asFile.get()
    modFile.releaseType = project.ext.versionBasedReleaseType
    modFile.displayName = "$archivesBaseName-$version" as String
    modFile.addJavaVersion 'Java 17'
    modFile.addModLoader 'Forge'
    modFile.addGameVersion "$mc_version"

    dependsOn(tasks.fullJar)
    finalizedBy(':makeReadme')
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(System.getenv('SONATYPE_USER') ?: '')
            password.set(System.getenv('SONATYPE_PASSWORD') ?: '')
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        }
    }
}

def sharedMetadata(MavenPom it) {
    it.packaging = 'jar'
    it.url = 'https://github.com/GroovyMC/GroovyModLoader'
    it.inceptionYear = '2022'
    it.licenses {
        license {
            name = 'MIT'
            url = 'https://opensource.org/license/mit/'
        }
    }
    it.developers {
        developer {
            id = 'groovymc'
            name = 'GroovyMC'
            email = 'holdings@groovymc.org'
            url = 'https://github.com/GroovyMC/'
        }
    }
    it.scm {
        connection='scm:git:git://github.com/GroovyMC/GroovyModLoader.git'
        url='https://github.com/GroovyMC/GroovyModLoader'
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            it.artifacts = [
                    jar, sourcesJar, fullJar, groovydocJar
            ]
            it.artifactId = 'gml'
            changelog.addArtifact(it)

            pom {
                name = 'GML'
                description = 'A language provider for Forge mods for Groovy'
                sharedMetadata(it)
                withXml { XmlProvider xml ->
                    final element = xml.asElement()
                    var depsElem = element.getOwnerDocument().createElement('dependencies')
                    var owner = element.getOwnerDocument()
                    var deps = (DependencySet) project.configurations.groovy.getDependencies()
                    deps.each {
                        depsElem.appendChild createDependency(owner, it.group, it.name, it.version, 'compile')
                    }
                    depsElem.appendChild createDependency(owner, project.group, 'transform', project.version, 'compile')
                    element.appendChild(depsElem)
                }
            }
        }
        register('transform', MavenPublication) {
            it.artifactId = 'transform'
            it.artifacts = [transformJar, transformSources, transformJavadocJar]
            pom {
                name = 'GML - Transform'
                description = 'Compile-time transforms for GML'
                sharedMetadata(it)
                withXml { XmlProvider xml ->
                    final element = xml.asElement()
                    var depsElem = element.getOwnerDocument().createElement('dependencies')
                    var owner = element.getOwnerDocument()
                    var deps = (DependencySet) project.configurations.groovy.getDependencies()
                    deps.each {
                        depsElem.appendChild createDependency(owner, it.group, it.name, it.version, 'compile')
                    }
                    element.appendChild(depsElem)
                }
            }
        }
    }
}

if (System.getenv('SONATYPE_USER')) {
    signing {
        final signingKey = System.getenv('SIGNING_KEY') ?: ''
        final signingPassword = System.getenv('SIGNING_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
        sign publishing.publications.transform
    }
}

@CompileStatic
static Element createDependency(Document owner, String group, String name, String version, String scope) {
    var sub = owner.createElement('dependency')

    var groupEl = owner.createElement('groupId')
    groupEl.appendChild(owner.createTextNode(group))
    sub.appendChild groupEl

    var artEl = owner.createElement('artifactId')
    artEl.appendChild(owner.createTextNode(name))
    sub.appendChild artEl

    var verEl = owner.createElement('version')
    verEl.appendChild(owner.createTextNode(version))
    sub.appendChild verEl

    var scopeEl = owner.createElement('scope')
    scopeEl.appendChild(owner.createTextNode(scope))
    sub.appendChild scopeEl

    return sub
}