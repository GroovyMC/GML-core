import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.cadixdev.licenser' version '0.6.1'
}

group = 'com.matyrobbrt.gml'
archivesBaseName = 'script-mods'

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.minecraftforge.net'
    }
    maven {
        url = 'https://libraries.minecraft.net/'
    }
}

configurations {
    shadow
}

dependencies {
    implementation 'cpw.mods:securejarhandler:2.1.4'
    implementation 'net.minecraftforge:forgespi:6.0.0'
    implementation 'com.electronwill.night-config:toml:3.6.4'
    implementation "org.apache.groovy:groovy:${groovy_version}"
    implementation "net.minecraftforge:fmlcore:${mc_version}-${forge_version}"
    implementation "net.minecraftforge:fmlloader:${mc_version}-${forge_version}"

    shadow('com.google.jimfs:jimfs:1.2') {
        exclude module: 'guava'
        exclude module: 'jsr305'
        exclude module: 'checker-compat-qual'
        exclude module: 'auto-service-annotation'
    }
    implementation'com.google.jimfs:jimfs:1.2'
}

shadowJar {
    configurations = [project.configurations.shadow]
    // Exclude the FS service as it will screw us in dev
    exclude 'META-INF/services/java.nio.file.spi.FileSystemProvider'

    // TODO - exclude these deps when we get them to not be relocated
    // dependencies {
    //    exclude(dependency('com.google.code.gson:gson:.*'))
    //    exclude(dependency('com.google.guava:guava:.*'))
    // }
}

tasks.register('relocateShadowJar', ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = 'com.matyrobbrt.gml.scriptmods.shadow'
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

jar {
    manifest.attributes([
            'Automatic-Module-Name': 'com.matyrobbrt.gml.scriptmods',
            'FMLModType': 'LIBRARY',
            'Implementation-Name': 'GScriptMods',
            'Implementation-Version': project.version
    ])
}